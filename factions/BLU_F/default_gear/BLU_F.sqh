#include "\ice\ice_gear\kitDefines.sqh"

private ["_result", "_role", "_side", "_faction",
	"_weapons", "_attachments", "_magazines", "_commonOrdnance", 
	"_uniform", "_vest", "_backpack", 
	"_uniformItems", "_vestItems", "_backpackItems", 
	"_assignedItems", "_helmet", "_glasses", "_nvGoggles", "_binoculars", "_nvGogglesForAll"];

_role = _this select 0;

// Default Gear

	_uniform = "rhs_uniform_FROG01_wd";
	_helmet = "rhsusf_mich_helmet_marpatwd";
	_vest = "rhsusf_spc_rifleman";
	_glasses = "rhs_googles_clear";
	_backpack = "";

	_weapons = ["rhs_weap_m4a1_grip"];
	_attachments = [["rhsusf_acc_compm4", "rhsusf_acc_SF3P556"], [], []];
	_magazines = [["rhs_mag_30Rnd_556x45_M855A1_Stanag", 5], ["rhs_mag_30Rnd_556x45_M855A1_Stanag_Tracer_Red", 2]];

	_uniformItems = [];
	_vestItems = [];
	_backpackItems = [];		
	_nvGoggles = if (__isNight) then {"NVGoggles";} else {"";};
	_binoculars = "Binocular";
	_assignedItems = [
		"ItemMap", 
		"ItemRadio", 
		"ItemCompass", 
		"ItemWatch"
	];
	_commonOrdnance = [
		["rhs_mag_m67", 2],
		["SmokeShell", 2],
		["ICE_FieldDressing", 5],
		["ICE_Adrenaline", 2],
		["ICE_Morphine", 2]
	];

switch (_role) do {

	case __ICE_gear_Rifleman: {
	
	};
		
	case __ICE_gear_Grenadier: {
	
		_vest = "rhsusf_spc_teamleader";
		
		_weapons = ["rhs_weap_m4a1_m203"];
		_magazines = if (__isNight) then {[["rhs_mag_30Rnd_556x45_M855A1_Stanag", 5], ["rhs_mag_30Rnd_556x45_M855A1_Stanag_Tracer_Red", 2], ["rhs_mag_M441_HE", 6], ["rhs_mag_m661_green", 4]];} else {[["rhs_mag_30Rnd_556x45_M855A1_Stanag", 5], ["rhs_mag_30Rnd_556x45_M855A1_Stanag_Tracer_Red", 2], ["rhs_mag_M441_HE", 6], ["rhs_mag_m714_White", 4]];};
	};
	
	case __ICE_gear_MG: {
	
		_vest = "rhsusf_spc_mg";
		_backpack = "rhsusf_assault_eagleaiii_ocp";
		
		_weapons = ["rhs_weap_m240G"];
		_magazines = [["rhsusf_100Rnd_762x51", 3], ["rhsusf_100Rnd_762x51_m62_tracer", 2]];
		_attachments = [["rhsusf_acc_compm4"], [], []];
	};

	case __ICE_gear_Medic: {
	
		_vest = "rhsusf_spc_corpsman";
		
		_commonOrdnance = [
			["rhs_mag_m67", 2],
			["SmokeShell", 8],
			["ICE_FieldDressing", 20],
			["ICE_Adrenaline", 20],
			["ICE_Morphine", 10]
		];
	};
		
	case __ICE_gear_CombatEngineer: {
	
		_vest = "rhsusf_spc_iar";
		_backpack = "rhsusf_assault_eagleaiii_ocp";
		
		_weapons pushback "MineDetector";
		
		_backpackItems = [["DemoCharge_Remote_Mag", 4], ["Toolkit", 1]];
	};

	case __ICE_gear_LightAT: {
	
		_backpack = "rhsusf_assault_eagleaiii_ocp";
		
		_weapons pushback "rhs_weap_M136";
	};

	case __ICE_gear_HeavyAT: {
	
		_backpack = "B_Carryall_oli";
		
		_weapons pushback "rhs_weap_smaw_green";
		_magazines pushback ["rhs_mag_smaw_HEAA", 2];
		_attachments set [1, ["rhs_weap_optic_smaw"]];
	};

	case __ICE_gear_AA: {
	
		_backpack = "B_Carryall_oli";
		
		_weapons pushback "rhs_weap_fim92";
		_magazines pushback ["rhs_fim92_mag", 1];
	};

	case __ICE_gear_Marksman: {
	
		_helmet = "rhs_Booniehat_marpatwd";
		_vest = "rhsusf_spc_marksman";
		
		_weapons = ["rhs_weap_m14ebrri_leu"];
		_attachments = [["optic_SOS"], [], []];
		_magazines = [["rhsusf_20Rnd_762x51_m118_special_Mag", 8]];
	};

	case __ICE_gear_Sniper: {
	
		_helmet = "rhs_Booniehat_marpatwd";
		_vest = "rhsusf_spc_marksman";
		
		_weapons = ["rhs_weap_XM2010_wd"];
		_attachments = [["rhsusf_acc_premier", "rhsusf_acc_harris_bipod"], [], []];
		_magazines = [["rhsusf_5Rnd_300winmag_xm2010", 10]];
		
		_binoculars = "Rangefinder";
		_assignedItems pushback "ItemGPS";
	};
		
	case __ICE_gear_SL: {
	
		_helmet = "rhsusf_mich_helmet_marpatwd_headset";
		_vest = "rhsusf_spc_squadleader";
		_backpack = "tf_rt1523g_rhs";
		
		_attachments = [["rhsusf_acc_ACOG_RMR", "rhsusf_acc_SF3P556", "rhsusf_acc_anpeq15"], [], []];
		
		_binoculars = "TB_laserDesignatorNVG";
		_assignedItems pushback "ItemGPS";
		_commonOrdnance append [
			["SmokeShellGreen", 2],
			["SmokeShellRed", 2],
			["LaserBatteries",1]
		];
	};

	case __ICE_gear_Crewman: {
	
		_helmet = "rhsusf_cvc_green_helmet";
		_glasses = "G_Combat";
		_vest = "rhsusf_spc_crewman";
		_backpack = "tf_rt1523g_sage";
		
		_weapons = ["rhs_weap_m4"];
		_attachments = [[], [], []];
		_magazines = [["rhs_mag_30Rnd_556x45_M855A1_Stanag", 4]];
		
		_binoculars = "Rangefinder";
		_assignedItems pushback "ItemGPS";
		_commonOrdnance append [
			["SmokeShellGreen", 2],
			["SmokeShellRed", 2]
		];
	};
		
	case __ICE_gear_Pilot: {
	
		_uniform = "U_B_HeliPilotCoveralls";
		_helmet = "rhsusf_hgu56p";
		_glasses = "G_Aviator";
		_vest = "V_PlateCarrier1_blk";
		_backpack = "B_Parachute";
		
		_weapons = ["rhs_weap_m4"];
		_attachments = [[], [], []];
		_magazines = [["rhs_mag_30Rnd_556x45_M855A1_Stanag", 4]];
		
		_assignedItems pushback "ItemGPS";
		_commonOrdnance append [
			["SmokeShellGreen", 2],
			["SmokeShellRed", 2]
		];
	};

	case __ICE_gear_Diver: {
	
		_uniform = "U_B_Wetsuit";
		_glasses = "G_Diving";
		_vest = "V_RebreatherB";
		
		_weapons = ["rhs_weap_m4a1_blockII_KAC_wd"];
		_attachments = [["rhsusf_acc_compm4", "rhsusf_acc_nt4_black"], [], []];
		_magazines = [["rhs_mag_30Rnd_556x45_M855A1_Stanag_No_Tracer", 7]];
	
		_assignedItems pushback "ItemGPS";
	};
};

//-----------------------------------------------------------------------------
_nvGogglesForAll = [["gear", "NVGogglesForAll"], "west", 1, true] call ICE_fnc_getSetting;
if (_nvGogglesForAll == 0) then {_nvGoggles = "";};
//-----------------------------------------------------------------------------

//-----------------------------------------------------------------------------
_result = 
[
	_weapons,
	_attachments, // nested array
	_magazines, // nested array - in A3, all magazines should be in each container's items list.
	_commonOrdnance, // combined nested/single array - in A3, all magazines should be in each container's items list.

	_uniform,
	_vest,
	_backpack,

	_uniformItems,
	_vestItems,
	_backpackItems,

	(_assignedItems+
		(if (_nvGoggles == "") then {[]} else {[_nvGoggles]})+
		(if (_binoculars == "") then {[]} else {[_binoculars]})
	),
	_helmet,
	_glasses
];
//-----------------------------------------------------------------------------
_result
